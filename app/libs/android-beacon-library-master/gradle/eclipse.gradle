// tasks for creating an eclipse bundle
task unzipAar(type: Copy, dependsOn: build) {
    description = 'Unzip the aar in order to create an eclipse project'

    from zipTree(file("$buildDir/outputs/aar/${project.name}-release.aar"))
    into file("$buildDir/outputs/aar/android-beacon-library")
}

task bundleEclipse(type: Tar, dependsOn: unzipAar) {
    description = 'Creates a tar file for eclipse distributions'

    destinationDir = file("$buildDir/outputs/aar/")
    extension = 'tar.gz'
    compression = Compression.GZIP
    includeEmptyDirs = true

    from("$buildDir/outputs/aar/android-beacon-library") {
        into 'android-beacon-library'
        exclude '*.jar'
    }

    from("$buildDir/outputs/aar/android-beacon-library") {
        into 'android-beacon-library/libs'
        include '*.jar'
    }

    from("$rootDir/eclipse-support/") {
        into 'android-beacon-library/'
        exclude '**/.retain'
    }
}
